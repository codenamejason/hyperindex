let config: Postgres.poolConfig = {
  ...Config.db,
  transform: {undefined: Js.null},
}
let sql = Postgres.makeSql(~config)

type chainId = int
type eventId = string
type blockNumberRow = {
  @as("block_number") blockNumber: int
}

module ChainMetadata = {
  type chainMetadata = {
    @as("chain_id") chainId: int,
    @as("block_height") blockHeight: int,
    @as("start_block") startBlock: int,
    @as("end_block") endBlock: option<int>,
    @as("first_event_block_number") firstEventBlockNumber: option<int>,
    @as("latest_processed_block") latestProcessedBlock: option<int>,
    @as("num_events_processed") numEventsProcessed: option<int>,
    @as("is_hyper_sync") isHyperSync: bool,
    @as("num_batches_fetched") numBatchesFetched: int,
    @as("latest_fetched_block_number") latestFetchedBlockNumber: int,
    @as("timestamp_caught_up_to_head_or_endblock") timestampCaughtUpToHeadOrEndblock: option<Js.Date.t>
  }

  @module("./DbFunctionsImplementation.js")
  external setChainMetadataBlockHeight: (Postgres.sql, chainMetadata) => promise<unit> =
    "setChainMetadataBlockHeight"
  @module("./DbFunctionsImplementation.js")
  external batchSetChainMetadata: (Postgres.sql, array<chainMetadata>) => promise<unit> =
    "batchSetChainMetadata"

  @module("./DbFunctionsImplementation.js")
  external readLatestChainMetadataState: (
    Postgres.sql,
    ~chainId: int,
  ) => promise<array<chainMetadata>> = "readLatestChainMetadataState"

  let setChainMetadataBlockHeightRow = (~chainMetadata: chainMetadata) => {
    sql->setChainMetadataBlockHeight(chainMetadata)
  }

  let batchSetChainMetadataRow = (~chainMetadataArray: array<chainMetadata>) => {
    sql->batchSetChainMetadata(chainMetadataArray)
  }

  let getLatestChainMetadataState = async (~chainId) => {
    let arr = await sql->readLatestChainMetadataState(~chainId)
    arr->Belt.Array.get(0)
  }
}

module EventSyncState = {
  @genType 
  type eventSyncState = {
    @as("chain_id") chainId: int,
    @as("block_number") blockNumber: int,
    @as("log_index") logIndex: int,
    @as("transaction_index") transactionIndex: int,
    @as("block_timestamp") blockTimestamp: int,
  }
  @module("./DbFunctionsImplementation.js")
  external readLatestSyncedEventOnChainIdArr: (
    Postgres.sql,
    ~chainId: int,
  ) => promise<array<eventSyncState>> = "readLatestSyncedEventOnChainId"

  let readLatestSyncedEventOnChainId = async (sql, ~chainId) => {
    let arr = await sql->readLatestSyncedEventOnChainIdArr(~chainId)
    arr->Belt.Array.get(0)
  }

  let getLatestProcessedBlockNumber = async (~chainId) => {
    let latestEventOpt = await sql->readLatestSyncedEventOnChainId(~chainId)
    latestEventOpt->Belt.Option.map(event => event.blockNumber)
  }

  @module("./DbFunctionsImplementation.js")
  external batchSet: (Postgres.sql, array<eventSyncState>) => promise<unit> =
    "batchSetEventSyncState"
}

module RawEvents = {
  type rawEventRowId = (chainId, eventId)
  @module("./DbFunctionsImplementation.js")
  external batchSet: (Postgres.sql, array<Types.rawEventsEntity>) => promise<unit> =
    "batchSetRawEvents"

  @module("./DbFunctionsImplementation.js")
  external batchDelete: (Postgres.sql, array<rawEventRowId>) => promise<unit> =
    "batchDeleteRawEvents"

  @module("./DbFunctionsImplementation.js")
  external readEntities: (
    Postgres.sql,
    array<rawEventRowId>,
  ) => promise<array<Types.rawEventsEntity>> = "readRawEventsEntities"

  @module("./DbFunctionsImplementation.js")
  external getRawEventsPageGtOrEqEventId: (
    Postgres.sql,
    ~chainId: chainId,
    ~eventId: Ethers.BigInt.t,
    ~limit: int,
    ~contractAddresses: array<Ethers.ethAddress>,
  ) => promise<array<Types.rawEventsEntity>> = "getRawEventsPageGtOrEqEventId"

  @module("./DbFunctionsImplementation.js")
  external getRawEventsPageWithinEventIdRangeInclusive: (
    Postgres.sql,
    ~chainId: chainId,
    ~fromEventIdInclusive: Ethers.BigInt.t,
    ~toEventIdInclusive: Ethers.BigInt.t,
    ~limit: int,
    ~contractAddresses: array<Ethers.ethAddress>,
  ) => promise<array<Types.rawEventsEntity>> = "getRawEventsPageWithinEventIdRangeInclusive"

  ///Returns an array with 1 block number (the highest processed on the given chainId)
  @module("./DbFunctionsImplementation.js")
  external readLatestRawEventsBlockNumberProcessedOnChainId: (
    Postgres.sql,
    chainId,
  ) => promise<array<blockNumberRow>> = "readLatestRawEventsBlockNumberProcessedOnChainId"

  let getLatestProcessedBlockNumber = async (~chainId) => {
    let row = await sql->readLatestRawEventsBlockNumberProcessedOnChainId(chainId)

    row->Belt.Array.get(0)->Belt.Option.map(row => row.blockNumber)
  }
}

module DynamicContractRegistry = {
  type contractAddress = Ethers.ethAddress
  type dynamicContractRegistryRowId = (chainId, contractAddress)
  @module("./DbFunctionsImplementation.js")
  external batchSet: (Postgres.sql, array<Types.dynamicContractRegistryEntity>) => promise<unit> =
    "batchSetDynamicContractRegistry"

  @module("./DbFunctionsImplementation.js")
  external batchDelete: (Postgres.sql, array<dynamicContractRegistryRowId>) => promise<unit> =
    "batchDeleteDynamicContractRegistry"

  @module("./DbFunctionsImplementation.js")
  external readEntities: (
    Postgres.sql,
    array<dynamicContractRegistryRowId>,
  ) => promise<array<Types.dynamicContractRegistryEntity>> = "readDynamicContractRegistryEntities"

  type contractTypeAndAddress = {
    @as("contract_address") contractAddress: Ethers.ethAddress,
    @as("contract_type") contractType: string,
    @as("event_id") eventId: Ethers.BigInt.t,
  }

  ///Returns an array with 1 block number (the highest processed on the given chainId)
  @module("./DbFunctionsImplementation.js")
  external readDynamicContractsOnChainIdAtOrBeforeBlock: (
    Postgres.sql,
    ~chainId: chainId,
    ~startBlock: int,
  ) => promise<array<contractTypeAndAddress>> = "readDynamicContractsOnChainIdAtOrBeforeBlock"
}

type entityHistoryItem = {
  block_timestamp: int,
  chain_id: int,
  block_number: int,
  log_index: int,
  previous_block_timestamp: option<int>,
  previous_chain_id: option<int>,
  previous_block_number: option<int>,
  previous_log_index: option<int>,
  params: option<Js.Json.t>,
  entity_type: string,
  entity_id: string,
}

let entityHistoryItemSchema = S.object((. s) => {
  block_timestamp: s.field("block_timestamp", S.int),
  chain_id: s.field("chain_id", S.int),
  block_number: s.field("block_number", S.int),
  log_index: s.field("log_index", S.int),
  previous_block_timestamp: s.field("previous_block_timestamp", S.null(S.int)),
  previous_chain_id: s.field("previous_chain_id", S.null(S.int)),
  previous_block_number: s.field("previous_block_number", S.null(S.int)),
  previous_log_index: s.field("previous_log_index", S.null(S.int)),
  params: s.field("params", S.null(S.json)),
  entity_type: s.field("entity_type", S.string),
  entity_id: s.field("entity_id", S.string),
})

module EntityHistory = {
  @module("./DbFunctionsImplementation.js")
  external batchSetInternal: (
    Postgres.sql,
    ~entityHistoriesToSet: array<Js.Json.t>,
  ) => promise<unit> = "batchInsertEntityHistory"

  let batchSet = (sql, ~entityHistoriesToSet) => {
    //Encode null for for the with prev types so that it's not undefined
    batchSetInternal(
      sql,
      ~entityHistoriesToSet=entityHistoriesToSet->Belt.Array.map(v =>
        v->S.serializeOrRaiseWith(entityHistoryItemSchema)
      ),
    )
  }

  @module("./DbFunctionsImplementation.js")
  external deleteAllEntityHistoryAfterEventIdentifier: (
    Postgres.sql,
    ~eventIdentifier: Types.eventIdentifier,
  ) => promise<unit> = "deleteAllEntityHistoryAfterEventIdentifier"

  type rollbackDiffResponseRaw = {
    entity_type: Types.entityName,
    entity_id: string,
    chain_id: option<int>,
    block_timestamp: option<int>,
    block_number: option<int>,
    log_index: option<int>,
    val: option<Js.Json.t>,
  }

  let rollbackDiffResponseRawSchema = S.object((. s) => {
    entity_type: s.field("entity_type", Types.entityNameSchema),
    entity_id: s.field("entity_id", S.string),
    chain_id: s.field("chain_id", S.null(S.int)),
    block_timestamp: s.field("block_timestamp", S.null(S.int)),
    block_number: s.field("block_number", S.null(S.int)),
    log_index: s.field("log_index", S.null(S.int)),
    val: s.field("val", S.null(S.json)),
  })

  type previousEntity = {
    eventIdentifier: Types.eventIdentifier,
    entity: Types.entity,
  }

  type rollbackDiffResponse = {
    entityType: Types.entityName,
    entityId: string,
    previousEntity: option<previousEntity>,
  }

  let rollbackDiffResponse_decode = (json: Js.Json.t) => {
    json
    ->S.parseWith(. rollbackDiffResponseRawSchema)
    ->Belt.Result.flatMap(raw => {
      switch raw {
      | {
          val: Some(val),
          chain_id: Some(chainId),
          block_number: Some(blockNumber),
          block_timestamp: Some(blockTimestamp),
          log_index: Some(logIndex),
          entity_type,
        } =>
        entity_type
        ->Types.getEntityParamsDecoder(val)
        ->Belt.Result.map(entity => {
          let eventIdentifier: Types.eventIdentifier = {
            chainId,
            blockTimestamp,
            blockNumber,
            logIndex,
          }

          Some({entity, eventIdentifier})
        })
      | _ => Ok(None)
      }->Belt.Result.map(previousEntity => {
        entityType: raw.entity_type,
        entityId: raw.entity_id,
        previousEntity,
      })
    })
  }

  let rollbackDiffResponseArr_decode = (jsonArr: array<Js.Json.t>) => {
    jsonArr->Belt.Array.map(rollbackDiffResponse_decode)->Utils.mapArrayOfResults
  }

  @module("./DbFunctionsImplementation.js")
  external getRollbackDiffInternal: (
    Postgres.sql,
    ~blockTimestamp: int,
    ~chainId: int,
    ~blockNumber: int,
  ) => promise<array<Js.Json.t>> = "getRollbackDiff"

  let getRollbackDiff = (sql, ~blockTimestamp: int, ~chainId: int, ~blockNumber: int) =>
    getRollbackDiffInternal(sql, ~blockTimestamp, ~chainId, ~blockNumber)->Promise.thenResolve(
      rollbackDiffResponseArr_decode,
    )
}

{{#each entities as |entity|}}
module {{entity.name.capitalized}} = {
  open Types

  @module("./DbFunctionsImplementation.js")
  external batchSetRaw: (Postgres.sql, array<Js.Json.t>) => promise<unit> = "batchSet{{entity.name.capitalized}}"

  let batchSet = async (sql: Postgres.sql, entities: array<Types.{{entity.name.uncapitalized}}Entity>): unit => {
    switch entities->S.serializeOrRaiseWith(Types.{{entity.name.uncapitalized}}EntitiesSchema) {
    | raw => await sql->batchSetRaw(raw->(Obj.magic: Js.Json.t => array<Js.Json.t>))
    | exception S.Raised(e) =>
      let path = e.path->S.Path.toArray
      Logging.error({
        "err": e,
        "msg": "EE700: Failed to serialize entity {{entity.name.capitalized}} to database representation",
        "unparsed_entity": entities->Js.Array2.unsafe_get(
          path->Js.Array2.unsafe_get(0)->(Obj.magic: string => int),
        ),
      })
      S.Error.raise(e)
    }
  }

  @module("./DbFunctionsImplementation.js")
  external batchDelete: (Postgres.sql, array<Types.id>) => promise<(unit)> = "batchDelete{{entity.name.capitalized}}"

  @module("./DbFunctionsImplementation.js")
  external readEntitiesRaw: (Postgres.sql, array<Types.id>) => promise<array<Js.Json.t>> =
    "read{{entity.name.capitalized}}Entities"

  let readEntities = async (sql: Postgres.sql, ids: array<Types.id>): array<{{entity.name.uncapitalized}}Entity> => {
    let res = await readEntitiesRaw(sql, ids)
    switch res->S.parseAnyOrRaiseWith(Types.{{entity.name.uncapitalized}}EntitiesSchema) {
    | entity => entity
    | exception S.Raised(e) =>
      let path = e.path->S.Path.toArray
      Logging.error({
        "err": e,
        "msg": "EE700: Failed to parse row from database of entity {{entity.name.capitalized}}",
        "raw_unparsed_object": res->Js.Array2.unsafe_get(
          path->Js.Array2.unsafe_get(0)->(Obj.magic: string => int),
        ),
      })
      S.Error.raise(e)
    }
  }
}
{{/each}}
